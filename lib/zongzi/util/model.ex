defmodule Zongzi.Util.Model do
  @moduledoc """
  领域模型。

  通过 `use Zongzi.Util.Model, keys: [...], id_prefix: "xxx"` 自动生成：

  - 结构体定义
  - `new/1`
  - `validate/1`
  - `update/2`

  以及加载必要的辅助函数（属性标准化、ID 生成）。
  """

  @callback validate(model :: struct()) :: {:ok, struct()} | {:error, term()}
  @optional_callbacks [validate: 1]

  defmacro __using__(opts) do
    # 这里一般是代码编写出了问题，可以 raise
    keys = Keyword.fetch!(opts, :keys)
    id_prefix = Keyword.get(opts, :id_prefix)

    quote do
      import Zongzi.Helpers, only: [normalize_attrs: 2]
      import Zongzi.Util.ID, only: [generate_id: 1]

      @behaviour Zongzi.Util.Model

      @keys unquote(keys)
      defstruct @keys

      # ---- 自动生成的构造/修改函数 ----

      @doc "根据属性创建新的结构体。"
      def new(attrs) do
        with {:ok, normalized} <- normalize_attrs(attrs, @keys) do
          {id, other_attrs} = Map.pop(normalized, :id, generate_id(unquote(id_prefix)))

          struct(__MODULE__, Map.put(other_attrs, :id, id))
          |> validate()
        end
      end

      @doc "修改已有结构体的属性（不允许修改 ID）"
      def update(model, attrs) do
        with {:ok, normalized} <- normalize_attrs(attrs, @keys),
             :ok <- if(Map.has_key?(normalized, :id), do: {:error, :id_immutable}, else: :ok),
             new_model = struct(model, normalized) do
          validate(new_model)
        end
      end

      @impl true
      def validate(model), do: {:ok, model}
      defoverridable validate: 1
    end
  end
end
